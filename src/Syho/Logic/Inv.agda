--------------------------------------------------------------------------------
-- Proof rules on the impredicative invariant
--------------------------------------------------------------------------------

{-# OPTIONS --without-K --sized-types #-}

module Syho.Logic.Inv where

open import Base.Func using (_$_)
open import Base.Eq using (_â‰¡_; refl; â— Ë™_)
open import Base.Size using (ğ•Š; !; Â¡_; _$áµ€Ê°_)
open import Base.Prod using (_,_; -,_)
open import Base.Sum using (Ä©â‚_)
open import Base.Nat using (â„•)
open import Syho.Lang.Expr using (Addr; Type; Vâ‡’E)
open import Syho.Lang.Ktxred using (Redex; ğŸ°á´¿_; Ktx; _á´·â—_)
open import Syho.Logic.Prop using (WpKind; Name; Propâˆ; PropË‚âˆ; Â¡á´¾_; _âˆ§_; âŒœ_âŒâˆ§_;
  _âˆ—_; _-âˆ—_; [^_]á´º; &â±âŸ¨_âŸ©_; %â±âŸ¨_âŸ©_; static; _â†¦â±_; Basic; ^á¶»á´º-âœ”)
open import Syho.Logic.Core using (_âŠ¢[_]_; _âŠ¢[<_]_; Pers; Pers-â‡’â–¡; _Â»_; âˆƒ-elim;
  âˆƒ-intro; âˆ§-monoË¡; âˆ§-elimÊ³; âŠ¤âˆ§-intro; âˆ—-monoÊ³; âˆ—-comm; âˆ—-assocË¡; âˆ—-assocÊ³;
  ?âˆ—-comm; âˆ—?-comm; âˆ—â‡’âˆ§; âˆƒâˆ—-elim; -âˆ—-applyË¡; -âˆ—-const; PersË¡-âˆ§â‡’âˆ—)
open import Syho.Logic.Supd using (_âŠ¢[_][_]â‡›_; _áµ˜Â»áµ˜_; _áµ˜Â»_; â‡›-frameË¡; â‡›-frameÊ³)
open import Syho.Logic.Hor using (_âŠ¢[_][_]áµƒâŸ¨_âŸ©_; _âŠ¢[_]âºâŸ¨_âŸ©[_]_; _âŠ¢[<á´¾_]âŸ¨_âŸ©[_]_;
  _áµ˜Â»áµƒÊ°_; _áµƒÊ°Â»áµ˜_; _áµƒÊ°Â»_; ahor-frameÊ³; ahorâœ”-hor; hor<á´¾-map)
open import Syho.Logic.Mem using (ahor-ğŸ°)

-- Import and re-export
open import Syho.Logic.Judg public using (&â±-â‡’â–¡; &â±-resp-â–¡âˆ§; %â±-mono; %â±-eatË¡;
  &â±-new-rec; &â±-open; %â±-close)

private variable
  Î¹ :  ğ•Š
  P Q R :  Propâˆ
  PË‚ QË‚ :  PropË‚âˆ
  nm :  Name
  i :  â„•
  T U :  Type
  red :  Redex T
  X :  Setâ‚€
  QË™ RË™ :  X â†’  Propâˆ
  Î¸ :  Addr
  v :  X
  Îº :  WpKind
  K :  Ktx T U

abstract

  ------------------------------------------------------------------------------
  -- On the invariant and open invariant tokens

  -->  %â±-mono :  PË‚ .!  âŠ¢[< Î¹ ]  QË‚ .!  â†’   %â±âŸ¨ nm âŸ© QË‚  âŠ¢[ Î¹ ]  %â±âŸ¨ nm âŸ© PË‚

  instance

    -- An invariant token is persistent

    -->  &â±-â‡’â–¡ :  &â±âŸ¨ nm âŸ© PË‚  âŠ¢[ Î¹ ]  â–¡ &â±âŸ¨ nm âŸ© PË‚

    &â±-Pers :  Pers $ &â±âŸ¨ nm âŸ© PË‚
    &â±-Pers .Pers-â‡’â–¡ =  &â±-â‡’â–¡

  -- Modify an invariant token

  -->  &â±-resp-â–¡âˆ§ :  {{Basic R}}  â†’
  -->    R  âˆ§  PË‚ .!  âŠ¢[< Î¹ ]  QË‚ .!  â†’   R  âˆ§  QË‚ .!  âŠ¢[< Î¹ ]  PË‚ .!  â†’
  -->    â–¡ R  âˆ§  &â±âŸ¨ nm âŸ© PË‚  âŠ¢[ Î¹ ]  &â±âŸ¨ nm âŸ© QË‚

  &â±-resp-âˆ§ :  {{Pers R}}  â†’   {{Basic R}}  â†’
    R  âˆ§  PË‚ .!  âŠ¢[< Î¹ ]  QË‚ .!  â†’   R  âˆ§  QË‚ .!  âŠ¢[< Î¹ ]  PË‚ .!  â†’
    R  âˆ§  &â±âŸ¨ nm âŸ© PË‚  âŠ¢[ Î¹ ]  &â±âŸ¨ nm âŸ© QË‚
  &â±-resp-âˆ§ Râˆ§PâŠ¢Q Râˆ§QâŠ¢P =  âˆ§-monoË¡ Pers-â‡’â–¡ Â» &â±-resp-â–¡âˆ§ Râˆ§PâŠ¢Q Râˆ§QâŠ¢P

  &â±-resp-âˆ— :  {{Pers R}}  â†’   {{Basic R}}  â†’
    R  âˆ—  PË‚ .!  âŠ¢[< Î¹ ]  QË‚ .!  â†’   R  âˆ—  QË‚ .!  âŠ¢[< Î¹ ]  PË‚ .!  â†’
    R  âˆ—  &â±âŸ¨ nm âŸ© PË‚  âŠ¢[ Î¹ ]  &â±âŸ¨ nm âŸ© QË‚
  &â±-resp-âˆ— Râˆ—PâŠ¢Q Râˆ—QâŠ¢P =  âˆ—â‡’âˆ§ Â»
    &â±-resp-âˆ§ ((PersË¡-âˆ§â‡’âˆ— Â»_) $áµ€Ê° Râˆ—PâŠ¢Q) ((PersË¡-âˆ§â‡’âˆ— Â»_) $áµ€Ê° Râˆ—QâŠ¢P)

  &â±-resp :  PË‚ .!  âŠ¢[< Î¹ ]  QË‚ .!  â†’   QË‚ .!  âŠ¢[< Î¹ ]  PË‚ .!  â†’
             &â±âŸ¨ nm âŸ© PË‚  âŠ¢[ Î¹ ]  &â±âŸ¨ nm âŸ© QË‚
  &â±-resp PâŠ¢Q QâŠ¢P =  âŠ¤âˆ§-intro Â»
    &â±-resp-âˆ§ ((âˆ§-elimÊ³ Â»_) $áµ€Ê° PâŠ¢Q) ((âˆ§-elimÊ³ Â»_) $áµ€Ê° QâŠ¢P)

  -- Let an open invariant token eat a basic proposition

  -->  %â±-eatË¡ :  {{Basic Q}}  â†’
  -->    Q  âˆ—  %â±âŸ¨ nm âŸ© PË‚  âŠ¢[ Î¹ ]  %â±âŸ¨ nm âŸ© Â¡á´¾ (Q -âˆ— PË‚ .!)

  %â±-eatÊ³ :  {{Basic Q}} â†’  %â±âŸ¨ nm âŸ© PË‚  âˆ—  Q  âŠ¢[ Î¹ ]  %â±âŸ¨ nm âŸ© Â¡á´¾ (Q -âˆ— PË‚ .!)
  %â±-eatÊ³ =  âˆ—-comm Â» %â±-eatË¡

  -- Create &â±âŸ¨ nm âŸ© PË‚ by storing PË‚

  -->  &â±-new-rec :  &â±âŸ¨ nm âŸ© PË‚ -âˆ— PË‚ .!  âŠ¢[ Î¹ ][ i ]â‡›  &â±âŸ¨ nm âŸ© PË‚

  &â±-new :  PË‚ .!  âŠ¢[ Î¹ ][ i ]â‡›  &â±âŸ¨ nm âŸ© PË‚
  &â±-new =  -âˆ—-const Â» &â±-new-rec

  -- Use an invariant token

  -->  &â±-open :  &â±âŸ¨ nm âŸ© PË‚  âˆ—  [^ nm ]á´º  âŠ¢[ Î¹ ][ i ]â‡›  PË‚ .!  âˆ—  %â±âŸ¨ nm âŸ© PË‚

  -->  %â±-close :  PË‚ .!  âˆ—  %â±âŸ¨ nm âŸ© PË‚  âŠ¢[ Î¹ ][ i ]â‡›  [^ nm ]á´º

  &â±-use :  PË‚ .!  âˆ—  Q  âŠ¢[ Î¹ ][ i ]â‡›  PË‚ .!  âˆ—  R  â†’
            &â±âŸ¨ nm âŸ© PË‚  âˆ—  [^ nm ]á´º  âˆ—  Q  âŠ¢[ Î¹ ][ i ]â‡›  [^ nm ]á´º  âˆ—  R
  &â±-use Pâˆ—QâŠ¢â‡›Pâˆ—R =  âˆ—-assocÊ³ Â» â‡›-frameÊ³ &â±-open áµ˜Â»áµ˜ âˆ—?-comm Â»
    â‡›-frameÊ³ Pâˆ—QâŠ¢â‡›Pâˆ—R áµ˜Â»áµ˜ âˆ—-assocË¡ Â» ?âˆ—-comm Â»
    â‡›-frameË¡ %â±-close áµ˜Â» âˆ—-comm

  ahor-&â±-use :  PË‚ .!  âˆ—  Q  âŠ¢[ Î¹ ][ i ]áµƒâŸ¨ red âŸ© (Î» v â†’  PË‚ .!  âˆ—  RË™ v)  â†’
    &â±âŸ¨ nm âŸ© PË‚  âˆ—  [^ nm ]á´º  âˆ—  Q  âŠ¢[ Î¹ ][ i ]áµƒâŸ¨ red âŸ© Î» v â†’  [^ nm ]á´º  âˆ—  RË™ v
  ahor-&â±-use Pâˆ—QâŠ¢âŸ¨redâŸ©Pâˆ—Rv =  âˆ—-assocÊ³ Â» â‡›-frameÊ³ {i = 0} &â±-open áµ˜Â»áµƒÊ°
    âˆ—?-comm Â» ahor-frameÊ³ Pâˆ—QâŠ¢âŸ¨redâŸ©Pâˆ—Rv áµƒÊ°Â»áµ˜ Î» _ â†’ âˆ—-assocË¡ Â» ?âˆ—-comm Â»
    â‡›-frameË¡ {i = 0} %â±-close áµ˜Â» âˆ—-comm

  ------------------------------------------------------------------------------
  -- Static reference

  -- Hoare triple rules for â†¦â±

  ahor-â†¦â±-ğŸ° :  Î¸ â†¦â± (T , v)  âˆ—  [^ static ]á´º  âŠ¢[ Î¹ ][ i ]áµƒâŸ¨ ğŸ°á´¿_ {T} Î¸ âŸ© Î» u â†’
                 âŒœ u â‰¡ v âŒâˆ§  [^ static ]á´º
  ahor-â†¦â±-ğŸ° =  &â±-open {i = 0} áµ˜Â»áµƒÊ° ahor-frameÊ³ ahor-ğŸ° áµƒÊ°Â»áµ˜ Î» _ â†’
    âˆƒâˆ—-elim Î» uâ‰¡v â†’ %â±-close {PË‚ = Â¡á´¾ _} {i = 0} áµ˜Â» âˆƒ-intro uâ‰¡v

  hor-â†¦â±-ğŸ° :  P  âŠ¢[<á´¾ Î¹ ]âŸ¨ K á´·â— Vâ‡’E v âŸ©[ Îº ]  QË™  â†’
              Î¸ â†¦â± (T , v)  âˆ—  P  âŠ¢[ Î¹ ]âºâŸ¨ Ä©â‚ (-, K , ğŸ°á´¿_ {T} Î¸) âŸ©[ Îº ]  QË™
  hor-â†¦â±-ğŸ° PâŠ¢âŸ¨KvâŸ©Q =  ahorâœ”-hor {i = 0} ^á¶»á´º-âœ”
    (?âˆ—-comm Â» âˆ—-assocÊ³ Â» ahor-frameÊ³ ahor-â†¦â±-ğŸ° áµƒÊ°Â» Î» _ â†’ âˆƒâˆ—-elim Î» uâ‰¡v â†’
      âˆ—-monoÊ³ $ âˆƒ-intro uâ‰¡v)
    Î» v â†’ hor<á´¾-map (Î» big â†’ âˆƒ-elim Î»{ refl â†’ big }) PâŠ¢âŸ¨KvâŸ©Q

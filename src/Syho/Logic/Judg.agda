--------------------------------------------------------------------------------
-- Judgment in Syho
--------------------------------------------------------------------------------
-- Its contents are re-exported across Syho.Logic.Core, Supd, Ind, and Hor

{-# OPTIONS --without-K --sized-types #-}

module Syho.Logic.Judg where

open import Base.Level using (Level; â†‘_)
open import Base.Size using (Size; âˆ)
open import Base.Thunk using (Thunk; Â¡_; !)
open import Base.Func using (_âˆ˜_; _$_)
open import Base.Few using (âŠ¤)
open import Base.Eq using (_â‰¡_)
open import Base.Prod using (_Ã—_; _,_; -,_)
open import Base.Sum using (injâ‚€; injâ‚)
open import Base.Nat using (â„•; suc)
open import Base.List using (List)
open import Base.List.Nat using (rep; len)
open import Base.RatPos using (â„šâº)

open import Syho.Logic.Prop using (Prop'; PropË‚; âˆ€â‚Ë™; âˆƒâ‚Ë™; âˆ€â‚-syntax; âˆƒâ‚-syntax;
  âˆƒâ‚âˆˆ-syntax; _âˆ§_; âŠ¤'; _â†’'_; _âˆ—_; _-âˆ—_; â¤‡_; â–¡_; _â†ª[_]â‡›_; â—‹_; _â†¦âŸ¨_âŸ©_; _â†ªâŸ¨_âŸ©á´¾_;
  _â†ªâŸ¨_âŸ©áµ€[_]_; _â†¦_; _â†¦Ë¡_; Free; Basic)
open import Syho.Lang.Expr using (Addr; Type; â—¸_; Expr; ExprË‚; â–¶_; âˆ‡_; Val; val;
  Vâ‡’E; AnyVal; âŠ¤-val)
open import Syho.Lang.Ktxred using (â–¶á´¿_; ndá´¿; _â—á´¿_; ğŸ°á´¿_; _â†á´¿_; allocá´¿; freeá´¿;
  Ktx; _á´·â—_; _á´·|á´¿_; Val/Ktxred; val/ktxred)

--------------------------------------------------------------------------------
-- WpKind :  Weakest precondion kind

data  WpKind :  Setâ‚€  where
  -- Partial
  par :  WpKind
  -- Total, with a counter
  tot :  â„• â†’  WpKind

--------------------------------------------------------------------------------
-- JudgRes :  Result of a judgment

private variable
  T U V :  Type
  Î¹ :  Size

infix 3 [_]â‡›_ âºâŸ¨_âŸ©[_]_

data  JudgRes :  Setâ‚‚  where
  -- Just a proposition
  Pure :  Prop' âˆ â†’  JudgRes
  -- Under the super update
  [_]â‡›_ :  â„• â†’  Prop' âˆ â†’  JudgRes
  -- Weakest precondion, over Val/Ktxred
  âºâŸ¨_âŸ©[_]_ :  Val/Ktxred T â†’  WpKind â†’  (Val T â†’ Prop' âˆ) â†’  JudgRes

--------------------------------------------------------------------------------
-- P âŠ¢[ Î¹ ]* Jr :  Judgment

infix 2 _âŠ¢[_]*_ _âŠ¢[_]_ _âŠ¢[<_]_ _âŠ¢[_][_]â‡›_ _âŠ¢[<_][_]â‡›_ _âŠ¢[_]âºâŸ¨_âŸ©[_]_
  _âŠ¢[_]âºâŸ¨_âŸ©á´¾_ _âŠ¢[_]âºâŸ¨_âŸ©áµ€[_]_ _âŠ¢[_]âŸ¨_âŸ©[_]_ _âŠ¢[_]âŸ¨_âŸ©á´¾_ _âŠ¢[<_]âŸ¨_âŸ©á´¾_ _âŠ¢[_]âŸ¨_âŸ©áµ€[_]_
  _âŠ¢[<_]âŸ¨_âŸ©áµ€[_]_

-- Declaring _âŠ¢[_]*_

data  _âŠ¢[_]*_ :  Prop' âˆ â†’  Size â†’  JudgRes â†’  Setâ‚‚

-- âŠ¢[ ] :  Pure sequent

_âŠ¢[_]_ :  Prop' âˆ â†’  Size â†’  Prop' âˆ â†’  Setâ‚‚
P âŠ¢[ Î¹ ] Q =  P âŠ¢[ Î¹ ]* Pure Q

-- âŠ¢[< ] :  Pure sequent under thunk

_âŠ¢[<_]_ :  Prop' âˆ â†’  Size â†’  Prop' âˆ â†’  Setâ‚‚
P âŠ¢[< Î¹ ] Q =  Thunk (P âŠ¢[_] Q) Î¹

-- âŠ¢[ ][ ]â‡› etc. :  Super update

_âŠ¢[_][_]â‡›_ _âŠ¢[<_][_]â‡›_ :  Prop' âˆ â†’  Size â†’  â„• â†’  Prop' âˆ â†’  Setâ‚‚
P âŠ¢[ Î¹ ][ i ]â‡› Q =  P âŠ¢[ Î¹ ]* [ i ]â‡› Q
P âŠ¢[< Î¹ ][ i ]â‡› Q =  Thunk (P âŠ¢[_][ i ]â‡› Q) Î¹

-- âŠ¢[ ]âºâŸ¨ âŸ©[ ] etc. :  Hoare triple over Val/Ktxred

_âŠ¢[_]âºâŸ¨_âŸ©[_]_ :
  Prop' âˆ â†’  Size â†’  Val/Ktxred T â†’  WpKind â†’  (Val T â†’ Prop' âˆ) â†’  Setâ‚‚
P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©[ wÎº ] Qáµ› =  P âŠ¢[ Î¹ ]* âºâŸ¨ vk âŸ©[ wÎº ] Qáµ›

_âŠ¢[_]âºâŸ¨_âŸ©á´¾_ :  Prop' âˆ â†’  Size â†’  Val/Ktxred T â†’  (Val T â†’ Prop' âˆ) â†’  Setâ‚‚
P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©á´¾ Qáµ› =  P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©[ par ] Qáµ›

_âŠ¢[_]âºâŸ¨_âŸ©áµ€[_]_ :
  Prop' âˆ â†’  Size â†’  Val/Ktxred T â†’  â„• â†’  (Val T â†’ Prop' âˆ) â†’  Setâ‚‚
P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©áµ€[ i ] Qáµ› =  P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©[ tot i ] Qáµ›

-- âŠ¢[ ]âŸ¨ âŸ©[ ] etc. :  Hoare triple over Expr

_âŠ¢[_]âŸ¨_âŸ©[_]_ :
  Prop' âˆ â†’  Size â†’  Expr âˆ T â†’  WpKind â†’  (Val T â†’ Prop' âˆ) â†’  Setâ‚‚
P âŠ¢[ Î¹ ]âŸ¨ e âŸ©[ wÎº ] Qáµ› =  P âŠ¢[ Î¹ ]âºâŸ¨ val/ktxred e âŸ©[ wÎº ] Qáµ›

_âŠ¢[_]âŸ¨_âŸ©á´¾_ _âŠ¢[<_]âŸ¨_âŸ©á´¾_ :
  Prop' âˆ â†’  Size â†’  Expr âˆ T â†’  (Val T â†’ Prop' âˆ) â†’  Setâ‚‚
P âŠ¢[ Î¹ ]âŸ¨ e âŸ©á´¾ Qáµ› =  P âŠ¢[ Î¹ ]âŸ¨ e âŸ©[ par ] Qáµ›
P âŠ¢[< Î¹ ]âŸ¨ e âŸ©á´¾ Qáµ› =  Thunk (P âŠ¢[_]âŸ¨ e âŸ©[ par ] Qáµ›) Î¹

_âŠ¢[_]âŸ¨_âŸ©áµ€[_]_ _âŠ¢[<_]âŸ¨_âŸ©áµ€[_]_ :
  Prop' âˆ â†’  Size â†’  Expr âˆ T â†’  â„• â†’  (Val T â†’ Prop' âˆ) â†’  Setâ‚‚
P âŠ¢[ Î¹ ]âŸ¨ e âŸ©áµ€[ i ] Qáµ› =  P âŠ¢[ Î¹ ]âŸ¨ e âŸ©[ tot i ] Qáµ›
P âŠ¢[< Î¹ ]âŸ¨ e âŸ©áµ€[ i ] Qáµ› =  Thunk (P âŠ¢[_]âŸ¨ e âŸ©áµ€[ i ] Qáµ›) Î¹

-- Pers :  Persistence of a proposition

record  Pers (P : Prop' âˆ) :  Setâ‚‚  where
  inductive
  -- Pers-â‡’â–¡ :  P can turn into â–¡ P
  field Pers-â‡’â–¡ :  P âŠ¢[ Î¹ ] â–¡ P
open Pers {{...}} public

private variable
  Å‚ :  Level
  i j n :  â„•
  X :  Set Å‚
  x :  X
  YË™ :  X â†’ Set Å‚
  Jr :  JudgRes
  P P' Q R :  Prop' âˆ
  PË™ QË™ :  X â†’ Prop' âˆ
  PË‚ P'Ë‚ QË‚ Q'Ë‚ RË‚ :  PropË‚ âˆ
  PË‚s :  List (PropË‚ âˆ)
  wÎº :  WpKind
  vk :  Val/Ktxred T
  Qáµ› Ráµ› :  Val T â†’ Prop' âˆ
  QË‚áµ› Q'Ë‚áµ› :  Val T â†’ PropË‚ âˆ
  e :  Expr âˆ U
  eË‚ :  ExprË‚ âˆ U
  eË™ :  X â†’ Expr âˆ U
  ktx :  Ktx T U
  v :  Val T
  Î¸ :  Addr
  p :  â„šâº
  av :  AnyVal
  avs :  List AnyVal

infixr -1 _Â»_ _áµ˜Â»áµ˜_ _áµ˜Â»Ê°_ _Ê°Â»áµ˜_

-- Defining _âŠ¢[_]*_

data  _âŠ¢[_]*_  where
  ------------------------------------------------------------------------------
  -- General rules

  -- The sequent is reflexive

  âŠ¢-refl :  P âŠ¢[ Î¹ ] P

  -- The left-hand side of a judgment can be modified with a sequent

  _Â»_ :  P âŠ¢[ Î¹ ] Q â†’  Q âŠ¢[ Î¹ ]* Jr â†’  P âŠ¢[ Î¹ ]* Jr

  ------------------------------------------------------------------------------
  -- On âˆ€ / âˆƒ

  -- Introducing âˆ€ / Eliminating âˆƒ

  âˆ€â‚-intro :  (âˆ€ x â†’  P âŠ¢[ Î¹ ] QË™ x) â†’  P âŠ¢[ Î¹ ] âˆ€â‚Ë™ QË™

  âˆƒâ‚-elim :  (âˆ€ x â†’  PË™ x âŠ¢[ Î¹ ]* Jr) â†’  âˆƒâ‚Ë™ PË™ âŠ¢[ Î¹ ]* Jr

  -- Eliminating âˆ€ / Introducing âˆƒ

  âˆ€â‚-elim :  âˆ€ x â†’  âˆ€â‚Ë™ PË™ âŠ¢[ Î¹ ] PË™ x

  âˆƒâ‚-intro :  âˆ€ x â†’  PË™ x âŠ¢[ Î¹ ] âˆƒâ‚Ë™ PË™

  -- Choice, which is safe to have thanks to the logic's predicativity

  choiceâ‚ :  âˆ€ {PË™Ë™ : âˆ€ (x : X) â†’ YË™ x â†’ Prop' âˆ} â†’
    âˆ€â‚ x , âˆƒâ‚ y , PË™Ë™ x y âŠ¢[ Î¹ ] âˆƒâ‚ yË™ âˆˆ (âˆ€ x â†’ YË™ x) , âˆ€â‚ x , PË™Ë™ x (yË™ x)

  ------------------------------------------------------------------------------
  -- On â†’

  -- â†’ is the right adjoint of âˆ§

  â†’-intro :  P âˆ§ Q âŠ¢[ Î¹ ] R â†’  Q âŠ¢[ Î¹ ] P â†’' R

  â†’-elim :  Q âŠ¢[ Î¹ ] P â†’' R â†’  P âˆ§ Q âŠ¢[ Î¹ ] R

  ------------------------------------------------------------------------------
  -- On âˆ—

  -- âˆ— is unital w.r.t. âŠ¤', commutative, associative, and monotone

  âŠ¤âˆ—-elim :  âŠ¤' âˆ— P âŠ¢[ Î¹ ] P

  âŠ¤âˆ—-intro :  P âŠ¢[ Î¹ ] âŠ¤' âˆ— P

  âˆ—-comm :  P âˆ— Q âŠ¢[ Î¹ ] Q âˆ— P

  âˆ—-assocË¡ :  (P âˆ— Q) âˆ— R âŠ¢[ Î¹ ] P âˆ— (Q âˆ— R)

  âˆ—-monoË¡ :  P âŠ¢[ Î¹ ] Q â†’  P âˆ— R âŠ¢[ Î¹ ] Q âˆ— R

  ------------------------------------------------------------------------------
  -- On -âˆ—

  -- -âˆ— is the right adjoint of âˆ—

  -âˆ—-intro :  P âˆ— Q âŠ¢[ Î¹ ] R â†’  Q âŠ¢[ Î¹ ] P -âˆ— R

  -âˆ—-elim :  Q âŠ¢[ Î¹ ] P -âˆ— R â†’  P âˆ— Q âŠ¢[ Î¹ ] R

  ------------------------------------------------------------------------------
  -- On â¤‡

  -- â¤‡ is monadic :  monotone, increasing, and idempotent

  â¤‡-mono :  P âŠ¢[ Î¹ ] Q â†’  â¤‡ P âŠ¢[ Î¹ ] â¤‡ Q

  â¤‡-intro :  P âŠ¢[ Î¹ ] â¤‡ P

  â¤‡-join :  â¤‡ â¤‡ P âŠ¢[ Î¹ ] â¤‡ P

  -- âˆ— can get inside â¤‡

  â¤‡-eatË¡ :  P âˆ— â¤‡ Q âŠ¢[ Î¹ ] â¤‡ (P âˆ— Q)

  -- âˆƒ -, can get outside â¤‡

  â¤‡-âˆƒâ‚-out :  â¤‡ (âˆƒâ‚ _ âˆˆ X , P) âŠ¢[ Î¹ ] âˆƒâ‚ _ âˆˆ X , â¤‡ P

  ------------------------------------------------------------------------------
  -- On â–¡

  -- â–¡ is comonadic :  monotone, decreasing, and idempotent

  â–¡-mono :  P âŠ¢[ Î¹ ] Q â†’  â–¡ P âŠ¢[ Î¹ ] â–¡ Q

  â–¡-elim :  â–¡ P âŠ¢[ Î¹ ] P

  â–¡-dup :  â–¡ P âŠ¢[ Î¹ ] â–¡ â–¡ P

  -- âˆ§ can turn into âˆ— when one argument is under â–¡

  â–¡Ë¡-âˆ§â‡’âˆ— :  â–¡ P âˆ§ Q âŠ¢[ Î¹ ] â–¡ P âˆ— Q

  -- âˆ€ can get inside â–¡

  â–¡-âˆ€â‚-in :  âˆ€â‚Ë™ (â–¡_ âˆ˜ PË™) âŠ¢[ Î¹ ] â–¡ âˆ€â‚Ë™ PË™

  -- âˆƒ can get outside â–¡

  â–¡-âˆƒâ‚-out :  â–¡ âˆƒâ‚Ë™ PË™ âŠ¢[ Î¹ ] âˆƒâ‚Ë™ (â–¡_ âˆ˜ PË™)

  ------------------------------------------------------------------------------
  -- On â‡›

  -- Increment the counter of â‡› by 1

  â‡›-suc :  P âŠ¢[ Î¹ ][ i ]â‡› Q â†’  P âŠ¢[ Î¹ ][ suc i ]â‡› Q

  -- Lift âŠ¢ â¤‡ to âŠ¢â‡›

  â¤‡â‡’â‡› :  P âŠ¢[ Î¹ ] â¤‡ Q â†’  P âŠ¢[ Î¹ ][ i ]â‡› Q

  -- âŠ¢â‡› is transitive

  _áµ˜Â»áµ˜_ :  P âŠ¢[ Î¹ ][ i ]â‡› Q â†’  Q âŠ¢[ Î¹ ][ i ]â‡› R â†’  P âŠ¢[ Î¹ ][ i ]â‡› R

  -- âŠ¢â‡› can frame

  â‡›-frameË¡ :  Q âŠ¢[ Î¹ ][ i ]â‡› R â†’  P âˆ— Q âŠ¢[ Î¹ ][ i ]â‡› P âˆ— R

  ------------------------------------------------------------------------------
  -- On â—‹

  -- â—‹ is monotone

  â—‹-mono :  PË‚ .! âŠ¢[< Î¹ ] QË‚ .! â†’  â—‹ PË‚ âŠ¢[ Î¹ ] â—‹ QË‚

  -- â—‹ can eat a basic proposition

  â—‹-eatË¡ :  {{Basic Q}} â†’  Q âˆ— â—‹ PË‚ âŠ¢[ Î¹ ] â—‹ Â¡ (Q âˆ— PË‚ .!)

  -- â—‹ P can be obtained by allocating P

  â—‹-alloc :  PË‚ .! âŠ¢[ Î¹ ][ i ]â‡› â—‹ PË‚

  -- When P is persistent, â–¡ â—‹ P_i can be obtained recursively, i.e.,
  -- by allocating P minus the target â–¡ â—‹ P

  â–¡â—‹-alloc-rec :  {{Pers (PË‚ .!)}} â†’  â–¡ â—‹ PË‚ -âˆ— PË‚ .! âŠ¢[ Î¹ ][ i ]â‡› â–¡ â—‹ PË‚

  -- Use â—‹ P

  â—‹-use :  â—‹ PË‚ âŠ¢[ Î¹ ][ i ]â‡› PË‚ .!

  ------------------------------------------------------------------------------
  -- On â†ªâ‡›

  -- Modify â‡› proof

  â†ªâ‡›-suc :  PË‚ â†ª[ i ]â‡› QË‚  âŠ¢[ Î¹ ]  PË‚ â†ª[ suc i ]â‡› QË‚

  â†ªâ‡›-eatË¡â»Ë¡áµ˜ :  {{Basic R}} â†’  R âˆ— P'Ë‚ .! âŠ¢[< Î¹ ][ i ]â‡› PË‚ .! â†’
                R âˆ— (PË‚ â†ª[ i ]â‡› QË‚)  âŠ¢[ Î¹ ]  P'Ë‚ â†ª[ i ]â‡› QË‚

  â†ªâ‡›-eatË¡â»Ê³ :  {{Basic R}} â†’
    R âˆ— (PË‚ â†ª[ i ]â‡› QË‚)  âŠ¢[ Î¹ ]  PË‚ â†ª[ i ]â‡› Â¡ (R âˆ— QË‚ .!)

  â†ªâ‡›-monoÊ³áµ˜ :  QË‚ .! âŠ¢[< Î¹ ][ i ]â‡› Q'Ë‚ .! â†’
               PË‚ â†ª[ i ]â‡› QË‚  âŠ¢[ Î¹ ]  PË‚ â†ª[ i ]â‡› Q'Ë‚

  â†ªâ‡›-frameË¡ :  PË‚ â†ª[ i ]â‡› QË‚  âŠ¢[ Î¹ ]  Â¡ (R âˆ— PË‚ .!) â†ª[ i ]â‡› Â¡ (R âˆ— QË‚ .!)

  -- Make â†ªâ‡› out of â—‹

  â—‹â‡’â†ªâ‡› :  PË‚ .! âˆ— RË‚ .! âŠ¢[< Î¹ ][ i ]â‡› QË‚ .! â†’  â—‹ RË‚  âŠ¢[ Î¹ ]  PË‚ â†ª[ i ]â‡› QË‚

  -- Use â†ªâ‡›, with counter increment
  ---- Without that counter increment, we could do any super update
  ---- (â‡›/â†ªâ‡›-use' in Syho.Logic.Paradox)

  â†ªâ‡›-use :  PË‚ .! âˆ— (PË‚ â†ª[ i ]â‡› QË‚)  âŠ¢[ Î¹ ][ suc i ]â‡›  QË‚ .!

  ------------------------------------------------------------------------------
  -- On â†ªâŸ¨ âŸ©á´¾

  -- Modify âŸ¨ âŸ©á´¾ proof

  â†ªâŸ¨âŸ©á´¾-eatË¡â»Ë¡áµ˜ :  {{Basic R}} â†’  (R âˆ— P'Ë‚ .! âŠ¢[< Î¹ ][ i ]â‡› PË‚ .!) â†’
                  R âˆ— (PË‚ â†ªâŸ¨ e âŸ©á´¾ QË‚áµ›)  âŠ¢[ Î¹ ]  P'Ë‚ â†ªâŸ¨ e âŸ©á´¾ QË‚áµ›

  â†ªâŸ¨âŸ©á´¾-eatË¡â»Ê³ :  {{Basic R}} â†’
    R âˆ— (PË‚ â†ªâŸ¨ e âŸ©á´¾ QË‚áµ›)  âŠ¢[ Î¹ ]  PË‚ â†ªâŸ¨ e âŸ©á´¾ Î» v â†’ Â¡ (R âˆ— QË‚áµ› v .!)

  â†ªâŸ¨âŸ©á´¾-monoÊ³áµ˜ :  (âˆ€ v â†’  QË‚áµ› v .! âŠ¢[< Î¹ ][ i ]â‡› Q'Ë‚áµ› v .!) â†’
                 PË‚ â†ªâŸ¨ e âŸ©á´¾ QË‚áµ›  âŠ¢[ Î¹ ]  PË‚ â†ªâŸ¨ e âŸ©á´¾ Q'Ë‚áµ›

  â†ªâŸ¨âŸ©á´¾-frameË¡ :  PË‚ â†ªâŸ¨ e âŸ©á´¾ QË‚áµ›  âŠ¢[ Î¹ ]
                   Â¡ (R âˆ— PË‚ .!) â†ªâŸ¨ e âŸ©á´¾ Î» v â†’ Â¡ (R âˆ— QË‚áµ› v .!)

  -- Make â†ªâŸ¨ âŸ©á´¾ out of â—‹

  â—‹â‡’â†ªâŸ¨âŸ©á´¾ :  PË‚ .! âˆ— RË‚ .! âŠ¢[< Î¹ ]âŸ¨ e âŸ©á´¾ (Î» v â†’ QË‚áµ› v .!) â†’
            â—‹ RË‚  âŠ¢[ Î¹ ]  PË‚ â†ªâŸ¨ e âŸ©á´¾ QË‚áµ›

  -- Use â†ªâŸ¨âŸ©á´¾, with â–¶ on the expression
  ---- Without that â–¶, we could have any partial Hoare triple
  ---- (horá´¾/â†ªâŸ¨âŸ©á´¾-use' in Syho.Logic.Paradox)

  â†ªâŸ¨âŸ©á´¾-use :  PË‚ .! âˆ— (PË‚ â†ªâŸ¨ e âŸ©á´¾ QË‚áµ›)  âŠ¢[ Î¹ ]âŸ¨ â–¶ Â¡ e âŸ©á´¾  Î» v â†’ QË‚áµ› v .!

  ------------------------------------------------------------------------------
  -- On â†ªâŸ¨ âŸ©áµ€

  -- Modify âŸ¨ âŸ©áµ€ proof

  â†ªâŸ¨âŸ©áµ€-suc :  PË‚ â†ªâŸ¨ e âŸ©áµ€[ i ] QË‚áµ›  âŠ¢[ Î¹ ]  PË‚ â†ªâŸ¨ e âŸ©áµ€[ suc i ] QË‚áµ›

  â†ªâŸ¨âŸ©áµ€-eatË¡â»Ë¡áµ˜ :  {{Basic R}} â†’  (R âˆ— P'Ë‚ .! âŠ¢[< Î¹ ][ j ]â‡› PË‚ .!) â†’
                  R âˆ— (PË‚ â†ªâŸ¨ e âŸ©áµ€[ i ] QË‚áµ›)  âŠ¢[ Î¹ ]  P'Ë‚ â†ªâŸ¨ e âŸ©áµ€[ i ] QË‚áµ›

  â†ªâŸ¨âŸ©áµ€-eatË¡â»Ê³ :  {{Basic R}} â†’
    R âˆ— (PË‚ â†ªâŸ¨ e âŸ©áµ€[ i ] QË‚áµ›)  âŠ¢[ Î¹ ]  PË‚ â†ªâŸ¨ e âŸ©áµ€[ i ] Î» v â†’ Â¡ (R âˆ— QË‚áµ› v .!)

  â†ªâŸ¨âŸ©áµ€-monoÊ³áµ˜ :  (âˆ€ v â†’  QË‚áµ› v .! âŠ¢[< Î¹ ][ j ]â‡› Q'Ë‚áµ› v .!) â†’
                 PË‚ â†ªâŸ¨ e âŸ©áµ€[ i ] QË‚áµ›  âŠ¢[ Î¹ ]  PË‚ â†ªâŸ¨ e âŸ©áµ€[ i ] Q'Ë‚áµ›

  â†ªâŸ¨âŸ©áµ€-frameË¡ :  PË‚ â†ªâŸ¨ e âŸ©áµ€[ i ] QË‚áµ›  âŠ¢[ Î¹ ]
                  Â¡ (R âˆ— PË‚ .!) â†ªâŸ¨ e âŸ©áµ€[ i ] Î» v â†’ Â¡ (R âˆ— QË‚áµ› v .!)

  -- Make â†ªâŸ¨ âŸ©áµ€ out of â—‹

  â—‹â‡’â†ªâŸ¨âŸ©áµ€ :  PË‚ .! âˆ— RË‚ .! âŠ¢[< Î¹ ]âŸ¨ e âŸ©áµ€[ i ] (Î» v â†’ QË‚áµ› v .!) â†’
            â—‹ RË‚  âŠ¢[ Î¹ ]  PË‚ â†ªâŸ¨ e âŸ©áµ€[ i ] QË‚áµ›

  -- Use â†ªâŸ¨âŸ©áµ€, with counter increment
  ---- Without that counter increment, we could have any total Hoare triple
  ---- (horáµ€/â†ªâŸ¨âŸ©áµ€-use' in Syho.Logic.Paradox)

  â†ªâŸ¨âŸ©áµ€-use :  PË‚ .! âˆ— (PË‚ â†ªâŸ¨ e âŸ©áµ€[ i ] QË‚áµ›)
                âŠ¢[ Î¹ ]âŸ¨ e âŸ©áµ€[ suc i ]  Î» v â†’ QË‚áµ› v .!

  ------------------------------------------------------------------------------
  -- On Hoare triple

  -- Weaken a Hoare triple from total to partial

  hor-áµ€â‡’á´¾ :  P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©áµ€[ i ] Qáµ› â†’  P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©á´¾ Qáµ›

  -- Counter increment on total Hoare triple

  horáµ€-suc :  P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©áµ€[ i ] Qáµ› â†’  P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©áµ€[ suc i ] Qáµ›

  -- Compose with a super update

  _áµ˜Â»Ê°_ :  P âŠ¢[ Î¹ ][ i ]â‡› Q â†’  Q âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©[ wÎº ] Ráµ› â†’
           P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©[ wÎº ] Ráµ›

  _Ê°Â»áµ˜_ :  P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©[ wÎº ] Qáµ› â†’  (âˆ€ v â†’  Qáµ› v âŠ¢[ Î¹ ][ i ]â‡› Ráµ› v) â†’
           P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©[ wÎº ] Ráµ›

  -- Frame

  hor-frameË¡ :  P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©[ wÎº ] Qáµ› â†’
                R âˆ— P âŠ¢[ Î¹ ]âºâŸ¨ vk âŸ©[ wÎº ] Î» v â†’ R âˆ— Qáµ› v

  -- Bind by a context

  hor-bind :
    P âŠ¢[ Î¹ ]âŸ¨ e âŸ©[ wÎº ] Qáµ› â†’  (âˆ€ v â†’  Qáµ› v âŠ¢[ Î¹ ]âŸ¨ ktx á´·â— Vâ‡’E v âŸ©[ wÎº ] Ráµ›) â†’
    P âŠ¢[ Î¹ ]âŸ¨ ktx á´·â— e âŸ©[ wÎº ] Ráµ›

  -- Value

  hor-valáµ˜ :  P âŠ¢[ Î¹ ][ i ]â‡› Qáµ› v â†’  P âŠ¢[ Î¹ ]âºâŸ¨ injâ‚€ v âŸ©[ wÎº ] Qáµ›

  -- Non-deterministic value

  hor-ndáµ˜ :  (âˆ€ x â†’  P âŠ¢[ Î¹ ][ i ]â‡› Qáµ› (val x)) â†’
             P âŠ¢[ Î¹ ]âºâŸ¨ injâ‚ $ ktx á´·|á´¿ ndá´¿ âŸ©[ wÎº ] Qáµ›

  -- â–¶, for partial and total Hoare triples

  horá´¾-â–¶ :  P âŠ¢[< Î¹ ]âŸ¨ ktx á´·â— eË‚ .! âŸ©á´¾ Qáµ› â†’
            P âŠ¢[ Î¹ ]âºâŸ¨ injâ‚ $ ktx á´·|á´¿ â–¶á´¿ eË‚ âŸ©á´¾ Qáµ›

  horáµ€-â–¶ :  P âŠ¢[ Î¹ ]âŸ¨ ktx á´·â— eË‚ .! âŸ©áµ€[ i ] Qáµ› â†’
            P âŠ¢[ Î¹ ]âºâŸ¨ injâ‚ $ ktx á´·|á´¿ â–¶á´¿ eË‚ âŸ©áµ€[ i ] Qáµ›

  -- Application

  hor-â— :  P âŠ¢[ Î¹ ]âŸ¨ ktx á´·â— eË™ x âŸ©[ wÎº ] Qáµ› â†’
           P âŠ¢[ Î¹ ]âºâŸ¨ injâ‚ $ ktx á´·|á´¿ eË™ â—á´¿ x âŸ©[ wÎº ] Qáµ›

  -- Memory read

  hor-ğŸ° :  Î¸ â†¦âŸ¨ p âŸ© (V , v) âˆ— P âŠ¢[ Î¹ ]âŸ¨ ktx á´·â— Vâ‡’E v âŸ©[ wÎº ] Qáµ› â†’
           Î¸ â†¦âŸ¨ p âŸ© (-, v) âˆ— P âŠ¢[ Î¹ ]âºâŸ¨ injâ‚ $ ktx á´·|á´¿ ğŸ°á´¿ Î¸ âŸ©[ wÎº ] Qáµ›

  -- Memory write

  hor-â† :  Î¸ â†¦ (V , v) âˆ— P âŠ¢[ Î¹ ]âŸ¨ ktx á´·â— âˆ‡ _ âŸ©[ wÎº ] Qáµ› â†’
           Î¸ â†¦ av âˆ— P âŠ¢[ Î¹ ]âºâŸ¨ injâ‚ $ ktx á´·|á´¿ Î¸ â†á´¿ v âŸ©[ wÎº ] Qáµ›

  -- Memory allocation

  hor-alloc :
    (âˆ€ Î¸ â†’  Î¸ â†¦Ë¡ rep n âŠ¤-val âˆ— Free n Î¸ âˆ— P âŠ¢[ Î¹ ]âŸ¨ ktx á´·â— âˆ‡ Î¸ âŸ©[ wÎº ] Qáµ›) â†’
    P âŠ¢[ Î¹ ]âºâŸ¨ injâ‚ $ ktx á´·|á´¿ allocá´¿ n âŸ©[ wÎº ] Qáµ›

  -- Memory freeing

  hor-free :
    len avs â‰¡ n â†’  P âŠ¢[ Î¹ ]âŸ¨ ktx á´·â— âˆ‡ _ âŸ©[ wÎº ] Qáµ› â†’
    Î¸ â†¦Ë¡ avs âˆ— Free n Î¸ âˆ— P âŠ¢[ Î¹ ]âºâŸ¨ injâ‚ $ ktx á´·|á´¿ freeá´¿ Î¸ âŸ©[ wÎº ] Qáµ›
